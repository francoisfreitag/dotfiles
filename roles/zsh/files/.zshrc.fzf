# Fuzzy finder
source <(fzf --zsh)
# https://github.com/catppuccin/fzf with the --tmux option.
export FZF_DEFAULT_OPTS=" \
--color=bg+:#313244,bg:#1e1e2e,spinner:#f5e0dc,hl:#f38ba8 \
--color=fg:#cdd6f4,header:#f38ba8,info:#cba6f7,pointer:#f5e0dc \
--color=marker:#b4befe,fg+:#cdd6f4,prompt:#cba6f7,hl+:#f38ba8 \
--color=selected-bg:#45475a \
--multi \
--tmux"

pytest_failed() {
    local TESTS
    TESTS=$(tmux capture-pane -S - -p \
        | rg '^(FAILED|ERROR) '\
        | tac \
        | cut -d' ' -f 2 \
        | sed -r '/^\s*$/d' \
        | awk '!a[$0]++' \
        | fzf --multi --bind 'ctrl-a:select-all+accept' \
        | while read -r item
        do
            echo -n -E "${(q)item} "
        done)
    if [[ -n $TESTS ]]; then
        zle push-line
        BUFFER="pytest ${TESTS}"
        zle accept-line
        local ret=$?
        zle reset-prompt
        return ret
    else
        zle redisplay
        return 0
    fi
}

zle -N pytest_failed
bindkey '^v' pytest_failed
